apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java-library-distribution'
apply plugin: org.owasp.dependencycheck.gradle.DependencyCheckPlugin

group = 'io.elastic'

task downloadDependencies() {
    description 'Download all dependencies to the Gradle cache'
    doLast {
        configurations.findAll { it.canBeResolved }.files
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        java.outputDir = file('build/classes/main')
    }
    test {
        java.outputDir = file('build/classes/test')
    }
}

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
    	url "https://repo1.maven.org/maven2"
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "io.elastic:sailor-jvm:3.3.9"
    compile "org.glassfish.jersey.core:jersey-client:2.25.1"
    compile "org.glassfish.jersey.media:jersey-media-json-processing:2.25.1"
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

check.dependsOn dependencyCheckAnalyze

dependencyCheck {
    format = 'ALL'
    // Dependency Check script will fail in case there are critical (9.0-10.0) vulnerabilities.
    // It should be configured to 7 (high and critical), but so far is not possible as 'axis' library
    // and log4j issues which does not have any updates that solve the problem
    failBuildOnCVSS = 7
    suppressionFile='./dependencyCheck-suppression.xml'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.0.3'
    }
}

wrapper {
    gradleVersion = '5.4.1'
}
